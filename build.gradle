
plugins {
	id 'application'
	id 'jacoco'
	id 'groovy'

	id 'com.palantir.git-version' version '0.15.0'

	id "com.github.ben-manes.versions" version "0.42.0"
}

wrapper.gradleVersion = '7.4.2'

group 'org.hansi_b'
version gitVersion()

repositories {
	mavenCentral()
	maven {
		name = 'GitHubPackages'
		url = 'https://maven.pkg.github.com/hansi-b/JavaSundries'
			credentials {
				username = project.findProperty("GITHUB_ACTOR") ?: ""
				password = project.findProperty("GITHUB_REPO_PAT_RO") ?: ""
			}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

dependencies {

	implementation 'org.hansib:sundries:0.1.4'

	testImplementation 'org.assertj:assertj-core:3.22.0'

	testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

	testImplementation 'org.objenesis:objenesis:3.2'
	testImplementation 'org.spockframework:spock-core:2.2-M1-groovy-4.0'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required=true
		html.required=false
	}
}

def mainClass = 'org.hansi_b.moss.SudokuCliReader'

jar {
	manifest {
		attributes 'Main-Class': mainClass
	}
}

run {
	mainClassName=mainClass
	standardInput = System.in
}
