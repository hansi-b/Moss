
plugins {
	id 'application'
	id 'jacoco'
	id 'groovy'

	id 'com.palantir.git-version' version '0.15.0'

	id "com.github.ben-manes.versions" version "0.42.0"
}

wrapper.gradleVersion = '7.5.1'

group 'org.hansi_b'
version gitVersion()

repositories {
	mavenCentral()
	maven {
		name = 'GitHubPackages'
		url = 'https://maven.pkg.github.com/hansi-b/JavaSundries'
		credentials {
			username = project.findProperty("GITHUB_ACTOR") ?: System.getenv("GITHUB_ACTOR")
			password = project.findProperty("GITHUB_REPO_PAT_RO") ?: System.getenv("GITHUB_REPO_PAT_RO")
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(19)
	}
}

dependencies {

	implementation 'org.hansib:sundries:0.4.0'

	testImplementation 'org.assertj:assertj-core:3.23.1'

	testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'

	testImplementation 'net.bytebuddy:byte-buddy:1.12.17'
	testImplementation 'org.objenesis:objenesis:3.3'
	testImplementation 'org.spockframework:spock-core:2.2-groovy-4.0'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required=true
		html.required=false
	}
}

def mainClass = 'org.hansi_b.moss.SudokuCliReader'

jar {
	manifest {
		attributes 'Main-Class': mainClass
	}
}

run {
	mainClassName=mainClass
	standardInput = System.in
}
