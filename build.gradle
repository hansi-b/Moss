
plugins {
	id 'maven-publish'
	id 'application'
	id 'jacoco'
	id 'groovy'

	id 'com.palantir.git-version' version '0.13.0'

	id "com.github.ben-manes.versions" version "0.41.0"
}

wrapper.gradleVersion = '7.3.3'

group 'org.hansi_b'
version gitVersion()

repositories {
	mavenCentral()
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}


publishing {

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'moss'
			from components.java
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/hansi-b/moss"
			credentials {
				username = project.hasProperty("GITHUB_ACTOR") ? GITHUB_ACTOR : ""
				password = project.hasProperty("GITHUB_REPO_PAT") ? GITHUB_REPO_PAT : ""
			}
		}
	}
}

dependencies {

	testImplementation 'org.assertj:assertj-core:3.22.0'
	testImplementation 'org.codehaus.groovy:groovy:3.0.9'

	testImplementation 'org.junit.vintage:junit-vintage-engine:5.8.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

	testImplementation 'org.objenesis:objenesis:3.2'
	testImplementation 'org.spockframework:spock-core:2.1-M2-groovy-3.0'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required=true
		html.required=false
	}
}

def mainClass = 'org.hansi_b.moss.SudokuCliReader'

jar {
	manifest {
		attributes 'Main-Class': mainClass
	}
}

run {
	mainClassName=mainClass
	standardInput = System.in
}
