
plugins {
	id 'maven-publish'
	id 'application'
	id 'jacoco'
	id 'groovy'

	id 'com.palantir.git-version' version '0.12.3'

	id "com.github.ben-manes.versions" version "0.33.0"
}

group 'org.hansi_b'
version gitVersion()

sourceCompatibility = 11

repositories {
	// Use jcenter for resolving your dependencies.
	// You can declare any Maven/Ivy/file repository here.
	jcenter()
}

publishing {

	publications {
		mavenJava(MavenPublication) {
			artifactId = 'moss'
			from components.java
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/hansi_b/moss"
			credentials {
				username = project.hasProperty("GITHUB_ACTOR") ? GITHUB_ACTOR : ""
				password = project.hasProperty("GITHUB_REPO_PAT") ? GITHUB_REPO_PAT : ""
			}
		}
	}
}

dependencies {

	testImplementation 'org.assertj:assertj-core:3.17.2'
	testImplementation 'org.codehaus.groovy:groovy:3.0.5'

	testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

	testImplementation 'org.objenesis:objenesis:3.1'
	testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-3.0'
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled false
	}
}

def mainClass = 'org.hansi_b.moss.SudokuCliReader'

jar {
	manifest {
		attributes 'Main-Class': mainClass
	}
}

run {
	mainClassName=mainClass
	standardInput = System.in
}
